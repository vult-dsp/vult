(*
   The MIT License (MIT)

   Copyright (c) 2014 Leonardo Laguna Ruiz, Carl JÃ¶nsson

   Permission is hereby granted, free of charge, to any person obtaining a copy
   of this software and associated documentation files (the "Software"), to deal
   in the Software without restriction, including without limitation the rights
   to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
   copies of the Software, and to permit persons to whom the Software is
   furnished to do so, subject to the following conditions:

   The above copyright notice and this permission notice shall be included in
   all copies or substantial portions of the Software.

   THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
   IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
   FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
   AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
   LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
   OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
   THE SOFTWARE.
*)
open Args
open Util
open Parser
open Core
open Generators

let getFile (args : args) (ext : Args.file_path) : string =
  match ext with
  | ExtOnly e -> args.output ^ "." ^ e
  | FullName n -> Filename.concat (Filename.dirname args.output) n


let showResult (args : args) (output : output) =
  match output with
  | Version v -> print_endline v
  | Message v -> print_endline v
  | Dependencies deps -> String.concat " " deps |> print_endline
  | ParsedCode v -> print_endline v
  | GeneratedCode files when args.output <> "" ->
      List.iter
        (fun (text, file) ->
          let filename = getFile args file in
          let code = Pla.print text in
          if args.force_write then
            FileIO.write filename code |> ignore
          else
            FileIO.writeIfDifferent filename code |> ignore)
        files
  | GeneratedCode files -> List.iter (fun (text, _) -> print_endline (Pla.print text)) files
  | Interpret v -> print_endline v
  | CheckOk -> ()
  | Errors errors ->
      let error_strings = Error.reportErrors errors in
      prerr_endline error_strings


let generate stmts = [ ParsedCode (Rust.generate stmts) ]

let generateCode (args : args) (parsed : Parse.parsed_file list) : output list =
  let env, typed = Inference.infer parsed in
  let stmts = Prog.convert env typed in
  let code =
    if args.code <> NoCode then
      generate stmts
    else
      []
  in
  let run =
    match args.eval with
    | Some e ->
        let s = Interpreter.run env stmts e in
        [ ParsedCode s ]
    | None -> []
  in
  code @ run


(** Prints the parsed files if -dparse was passed as argument *)
let dumpParsedFiles (args : args) (parsed : Parse.parsed_file list) : output list =
  if args.dparse then
    [ ParsedCode "not implemented" ]
  else
    []


let version = String.sub Version.version 1 (String.length Version.version - 2)

let driver (args : args) : output list =
  try
    if args.show_version then
      [ Version version ]
    else (* Parse the files *)
      match args.files with
      | [] -> [ Message ("vult " ^ version ^ " - https://github.com/modlfo/vult\nno input files") ]
      | _ ->
          let parsed = Loader.loadFiles args args.files in
          if args.deps then
            List.map (fun r -> r.Parse.file) parsed |> fun s -> [ Dependencies s ]
          else
            dumpParsedFiles args parsed @ generateCode args parsed
  with
  | Error.Errors errors -> [ Errors errors ]


let main () =
  let args = processArguments () in
  let results = driver args in
  List.iter (showResult args) results ;
  exit 0
