/*
The MIT License (MIT)

Copyright (c) 2017 Leonardo Laguna Ruiz
-----
State variable filter
*/

fun calc_g(cv) @[table(size=128, min=0.0, max=0.9)]{
   val pitch = Util.cvToPitch(cv);
   val f = 8.175798915643707*exp(0.057762265046662105*pitch);
   val pi = 3.141592653589793;
   val wd = 2.0*pi*f;
	val t  = 1.0/44100.0;
	val wa = (2.0/t)*tan(wd*t/2.0);
	val g  = wa*t/2.0;
   return g;
}

fun process(x, cv, q, sel) {
    mem z1, z2;
   mem g,inv_den, r;
   q = q + 0.5;
   if(Util.change(cv) || Util.change(q)) {
      g = calc_g(cv);
      r = 1.0/(2.0*(q+eps()));
      inv_den = 1.0/(1.0 + 2.0*r*g + g*g);
   }

   val high  = (x - (2.0*r + g)*z1 - z2) * inv_den;
	val band  = g*high + z1;
	val low   = g*band + z2;
   val notch = low + high;

	z1 = g*high + band;
	z2 = g*band + low;

   val output =
      if sel == 0 then low else
      if sel == 1 then high else
      if sel == 2 then band else
      notch;
   return Saturate_soft.process(output);
}
and noteOn(note:int,velocity:int,channel:int){ }
and noteOff(note:int,channel:int){ }
and controlChange(control:int,value:int,channel:int){ }
and default() @[init] {
   g = calc_g(0.0);
   r = 1.0/(2.0*0.5);
   inv_den = 1.0/(1.0 + 2.0*0.0*g + g*g);
 }