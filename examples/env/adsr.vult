/*
The MIT License (MIT)

Copyright (c) 2014-2024 Leonardo Laguna Ruiz
-----
Attack Decay Sustain Release Envelope.

To pass the parameters as direct values use the function 'do'.
For parameters as controls use 'process'.
*/

fun do(gate:real, a, d, s, r) {
   mem state;
   mem out;
   mem rate;
   mem target;
   mem scale;

   val a_rate = 1.0 / (100.0 * a + 0.01);
   val d_rate = 1.0 / (100.0 * d + 0.01);
   val r_rate = 1.0 / (100.0 * r + 0.01);

   // Magic numbers:
   // 0.001 : scales the effect of input parameters
   // 1024.0 : improves numeric precision in fixed-point

   out = out + (target - out) * rate * 0.004;

   val bgate = gate > 0.0;

   // idle // release
   if(state == 0) {
      if(Util.edge(bgate)) {
         state = 1; // goto attack
         scale = gate / 5.0;
      }
      rate = r_rate;
      target = 0.0;
   }
   // attack
   if(state == 1) {
      if(not(bgate)) state = 0; // goto release
      if(out > 1024.0) state = 2; // goto decay
      rate = a_rate;
      target = 1.2 * 1024.0;
   }
   // decay // sustain
   if (state == 2) {
      if(not(bgate)) state = 0; // goto release
      rate = d_rate;
      target = s * 1024.0;
   }

   return Util.smooth(scale) * clip(out/1024.0,0.0,1.0);
}

fun process(gate:real) {
   mem knob1, knob2, knob3, knob4;

   return do(gate,knob1,knob2,knob3,knob4);
}
and noteOn(note:int, velocity:int, channel:int) {
}
and noteOff(note:int, channel:int) {
}
and controlChange(control:int, value: int, channel:int) {
   if(control == 1)
      knob1 = real(value)/127.0;
   if(control == 2)
      knob2 = real(value)/127.0;
   if(control == 3)
      knob3 = real(value)/127.0;
   if(control == 4)
      knob4 = real(value)/127.0;
}
and default() {
   knob1 = 0.0;
   knob2 = 0.5;
   knob3 = 1.0;
   knob4 = 0.5;
}
